%% -*- erlang-indent-level: 4;indent-tabs-mode: nil; fill-column: 92 -*-
%% ex: ts=4 sw=4 et

query <- star_star / (expression / space)* %{
    ?i2b(Node)
%};

star_star <- "*:*" ~;

expression <- operation / group / field_phrase / field / field_range / term / string %{
           convert_final(Node)
%};

term <- keyword valid_letter+ / !keyword valid_letter+ %{
     [{term, Node}]
%};

field <- name:field_name ":" arg:(fuzzy_op / term / group / string) %{
    [<<"content:">>, {term, ?gv(name, Node)}, chef_solr:kv_sep(get(search_module)), ?gv(arg, Node)]
%};

field_phrase <- name:field_name ':' '"' str:(term (space term)*) '"' %{
    P = convert_phrase(?gv(str, Node)),
    [<<"content:\"">>, ?gv(name, Node), chef_solr:kv_sep(get(search_module)), P, <<"\"">>]
%};

field_range <- field_name ":" (("[" range_entry " TO " range_entry "]") /
                               ("{" range_entry " TO " range_entry "}")) %{
    [ FieldName, _, [Open, Start, _, End, Close] ] = Node,
    convert_range_expression(FieldName, Open, Start, End, Close)
%};

field_name <- !keyword valid_letter+ ~;

range_entry <- "*" / valid_letter+ ~;

group <- '(' query ')' ~;

operation <- binary_op / unary_op / fuzzy_op / boost_op ~;

binary_op <- lhs:(group / field_phrase / field  / field_range / term) space? op:bool_op space? rhs:query %{
    [?gv(lhs, Node), <<" ">>, ?gv(op, Node), <<" ">>, ?gv(rhs, Node)]
%};

bool_op <- 'AND' / '&&' / 'OR' / '||' ~;

unary_op <- not_op / required_op / prohibited_op ~;

not_op <- ('NOT' space / '!' space?) (group / field / field_range / term / string) ~;

required_op <- !valid_letter '+' (term / string) ~;

prohibited_op <- !valid_letter '-' (field / field_range / term / string) ~;

boost_op <- (term / string) '^' fuzzy_param ~;

fuzzy_op <- (term / string) '~' fuzzy_param? ~;

fuzzy_param <- [0-9]+ ('.' [0-9])? ~;

string <- '"' phr:(term (space term)*) '"' %{
       [ <<"\"">>, convert_phrase(?gv(phr, Node)), <<"\"">>]
%};

keyword <- "AND" / "OR" / "NOT" ~;

valid_letter <- start_letter+ ([a-zA-Z0-9*?@_+./-] / escaped_special)* ~;

start_letter <- [a-zA-Z0-9_.*/] / escaped_special ~;

escaped_special <- '\\' chr:special_char %{
    [{escaped_special, ?gv(chr, Node)}]
%};

space <- [ \t]+ ~;

special_char <- '[' / ']' / '\\' / '"' / [!(){}^~*?:] ~;

%{
%% @author Seth Falcon <seth@opscode.com>
%% Copyright 2011-2012 Opscode, Inc. All Rights Reserved.
%%
%% This file is provided to you under the Apache License,
%% Version 2.0 (the "License"); you may not use this file
%% except in compliance with the License.  You may obtain
%% a copy of the License at
%%
%%   http://www.apache.org/licenses/LICENSE-2.0
%%
%% Unless required by applicable law or agreed to in writing,
%% software distributed under the License is distributed on an
%% "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
%% KIND, either express or implied.  See the License for the
%% specific language governing permissions and limitations
%% under the License.
%%

-define(i2b(X), iolist_to_binary(X)).
-define(gv(X, L), proplists:get_value(X, L)).

% make sure these atoms are available.
-define(or_op1, 'OR').
-define(or_op2, '||').
-define(and_op1, 'AND').
-define(and_op2, '&&').
-define(not_op, 'NOT').
-define(not_bang_op, '!').

-spec convert_final(list()) -> iolist().
convert_final(Ast) ->
      L = lists:flatten(Ast),
      convert_final1(L, []).

convert_final1([], Acc) ->
  chef_solr:transform_query_safe(get(search_module), iolist_to_binary(lists:reverse(Acc)));
convert_final1([Bin|Rest], Acc) when is_binary(Bin) ->
   convert_final1(Rest, [Bin|Acc]);
convert_final1([{term, Content}|Rest], Acc) ->
   convert_final1(Rest, [convert_term(Content) | Acc]).

-spec convert_term(iolist()) -> binary().
convert_term(Term) ->
  L = lists:flatten(Term),
  convert_term1(L, [], []).

convert_term1([], Accum1, Accum2) ->
  Prev = chef_solr:transform_query_term(get(search_module), iolist_to_binary(lists:reverse(Accum1))),
  [Accum2, Prev];
convert_term1([Term|Rest], Accum1, Accum2) when is_binary(Term) ->
  convert_term1(Rest, [Term|Accum1], Accum2);
convert_term1([{escaped_special, Char}|Rest], Accum1, Accum2) ->
  Prev = convert_char(lists:reverse(Accum1)),
  NewAccum2 = [Accum2, Prev, <<"\\">>, convert_char(Char)],
  convert_term1(Rest, [], NewAccum2).

-spec convert_phrase(iolist()) -> binary().
convert_phrase(Phrase) ->
  convert_phrase1(lists:flatten(Phrase), []).

convert_phrase1([], Accum) ->
  chef_solr:transform_query_phrase(get(search_module), iolist_to_binary(lists:reverse(Accum)));
convert_phrase1([{term, Term}|Rest], Accum) ->
  convert_phrase1(Rest, [convert_term1(lists:flatten(Term), [], [])|Accum]);
convert_phrase1([Other|Rest], Accum) ->
  convert_phrase1(Rest, [Other|Accum]).


-spec convert_char(iolist()) -> binary().
convert_char(Char) ->
  chef_solr:transform_query_all(get(search_module), iolist_to_binary(Char)).

-spec convert_range_expression(binary(), binary(), binary(), binary(), binary()) -> iolist().
convert_range_expression(FieldName, _, <<"*">>, <<"*">>, _) ->
    [<<"content:">>, FieldName, chef_solr:kv_sep(get(search_module)), <<"*">>];

convert_range_expression(FieldName, Open, Start, <<"*">>, Close) ->
    [<<"content:">>,
     Open, FieldName, chef_solr:kv_sep(get(search_module)), Start,
     <<" TO ">>,
     FieldName, chef_solr:kv_sep(get(search_module)), <<"\\ufff0">>, Close];

convert_range_expression(FieldName, Open, <<"*">>, End, Close) ->
    [<<"content:">>,
     Open, FieldName, chef_solr:kv_sep(get(search_module)),
     <<" TO ">>,
     FieldName, chef_solr:kv_sep(get(search_module)), End, Close];

convert_range_expression(FieldName, Open, Start, End, Close) ->
    [<<"content:">>,
     Open, FieldName, chef_solr:kv_sep(get(search_module)), Start,
     <<" TO ">>,
     FieldName, chef_solr:kv_sep(get(search_module)), End, Close].

%}
